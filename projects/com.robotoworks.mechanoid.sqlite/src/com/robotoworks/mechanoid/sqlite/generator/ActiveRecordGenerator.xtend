package com.robotoworks.mechanoid.sqlite.generator

import com.robotoworks.mechanoid.sqlite.sqliteModel.CreateTableStatement
import com.robotoworks.mechanoid.sqlite.sqliteModel.Model

import static extension com.robotoworks.mechanoid.common.util.Strings.*
import static extension com.robotoworks.mechanoid.sqlite.generator.Extensions.*

class ActiveRecordGenerator {
		def CharSequence generate(Model model, CreateTableStatement stmt) '''
			/*
			 * Generated by Robotoworks Mechanoid
			 */
			package «model.packageName»;

			import java.util.ArrayList;
			import java.util.List;
			
			import android.content.ContentResolver;
			import android.content.ContentUris;
			import android.database.Cursor;
			import android.net.Uri;
			import android.os.Parcel;
			import android.os.Parcelable;
			
			import «model.packageName».«model.database.name.pascalize»Contract.«stmt.name.pascalize»;
			import «model.packageName».«model.database.name.pascalize»Contract.«stmt.name.pascalize».Builder;
			import com.robotoworks.mechanoid.sqlite.SQuery;
			import com.robotoworks.mechanoid.util.Closeables;
			import com.robotoworks.mechanoid.sqlite.ActiveRecord;
						
			public class «stmt.name.pascalize»Record extends ActiveRecord implements Parcelable {
			    public static final Parcelable.Creator<«stmt.name.pascalize»Record> CREATOR 
			    	= new Parcelable.Creator<«stmt.name.pascalize»Record>() {
			        public «stmt.name.pascalize»Record createFromParcel(Parcel in) {
			            return new «stmt.name.pascalize»Record(in);
			        }
			
			        public «stmt.name.pascalize»Record[] newArray(int size) {
			            return new «stmt.name.pascalize»Record[size];
			        }
			    };
			    
			    public static String[] PROJECTION = {
			    	«generateProjectionArrayMembers(stmt)»
			    };
			    
			    public interface Indices {
			    	«generateProjectionIndicesMembers(stmt)»
			    }
			    
			    «generateFields(stmt)»
			    «generateAccessors(stmt)»
			    
			    public «stmt.name.pascalize»Record() {
				}
				
				private «stmt.name.pascalize»Record(Parcel in) {
					«var counter=-1»
					«FOR col : stmt.columnDefs»
					m«col.name.pascalize» = in.read«col.type.toJavaTypeName.pascalize»();
					«ENDFOR»
					
					boolean[] dirtyFlags = new boolean[«stmt.columnDefs.size»];
					in.readBooleanArray(dirtyFlags);
					«FOR col : stmt.columnDefs»
					m«col.name.pascalize»Dirty = dirtyFlags[«counter = counter + 1»];
					«ENDFOR»
				}
				
				@Override
				public int describeContents() {
				    return 0;
				}
				
				@Override
				public void writeToParcel(Parcel dest, int flags) {
					«FOR col : stmt.columnDefs»
					dest.write«col.type.toJavaTypeName.pascalize»(m«col.name.pascalize»);
					«ENDFOR»
				    dest.writeBooleanArray(new boolean[] {
						«FOR col : stmt.columnDefs SEPARATOR ","»
						m«col.name.pascalize»Dirty
						«ENDFOR»
				    });
				}
				
				private Builder createBuilder() {
					Builder builder = «stmt.name.pascalize».newBuilder();

					«FOR col : stmt.columnDefs.filter([!it.name.equals("_id")])»
					if(m«col.name.pascalize»Dirty) {
						builder.set«col.name.pascalize»(m«col.name.pascalize»);
					}
					«ENDFOR»
					
					return builder;
				}
			    
			    @Override
				public long save(ContentResolver resolver){
					Builder builder = createBuilder();
					
					if(m_id > 0) {
					    builder.update(resolver, m_id);
					} else {
					    Uri uri = builder.insert(resolver);
					    m_id = ContentUris.parseId(uri);
					}
					
					return m_id;
				}
				
			    @Override
				public int update(ContentResolver resolver, SQuery query){
					Builder builder = createBuilder();
					
					return builder.update(resolver, query);
				}
				
			    @Override
				public boolean delete(ContentResolver resolver){
					return resolver.delete(
						«stmt.name.pascalize».CONTENT_URI.buildUpon()
						.appendPath(String.valueOf(m_id)).build(), null, null) > 0;
				}
				
				private static «stmt.name.pascalize»Record fromCursor(Cursor c) {
				    «stmt.name.pascalize»Record item = new «stmt.name.pascalize»Record();
				    
					«FOR col : stmt.columnDefs»
					item.m«col.name.pascalize» = c.get«col.type.toJavaTypeName.pascalize»(Indices.«col.name.underscore.toUpperCase»);
					«ENDFOR»
					
				    return item;
				}
				
				public static «stmt.name.pascalize»Record select(ContentResolver resolver, long id) {
				    Cursor c = null;
				    
				    try {
				        c = resolver.query(«stmt.name.pascalize».CONTENT_URI.buildUpon()
						.appendPath(String.valueOf(id)).build(), PROJECTION, null, null, null);
				        
				        if(!c.moveToFirst()) {
				            return null;
				        }
				        
				        return fromCursor(c);
				    } finally {
				        Closeables.closeSilently(c);
				    }
				}
				
				public static List<«stmt.name.pascalize»Record> select(ContentResolver resolver, SQuery query) {
				    return select(resolver, query, null);
				}
				
				public static List<«stmt.name.pascalize»Record> select(ContentResolver resolver, SQuery query, String sortOrder) {
				    Cursor c = null;
				    
				    ArrayList<«stmt.name.pascalize»Record> items = new ArrayList<«stmt.name.pascalize»Record>();
				    
				    try {
				        c = query.query(resolver, «stmt.name.pascalize».CONTENT_URI, PROJECTION, sortOrder);
				        
				        while(c.moveToNext()) {
				            items.add(fromCursor(c));
				        }
				    } finally {
				        Closeables.closeSilently(c);
				    }
				    
				    return items;
				}
				
				public static List<«stmt.name.pascalize»Record> select(ContentResolver resolver, String selection, String[] selectionArgs, String sortOrder) {
				    Cursor c = null;
				    
				    ArrayList<«stmt.name.pascalize»Record> items = new ArrayList<«stmt.name.pascalize»Record>();
				    
				    try {
				        c = resolver.query(«stmt.name.pascalize».CONTENT_URI, PROJECTION, selection, selectionArgs, sortOrder);
				        
				        while(c.moveToNext()) {
				            items.add(fromCursor(c));
				        }
				    } finally {
				        Closeables.closeSilently(c);
				    }
				    
				    return items;
				}
				
				public static List<«stmt.name.pascalize»Record> select(ContentResolver resolver, String selection, String[] selectionArgs) {
				    return select(resolver, selection, selectionArgs, null);
				}
				
				public static List<«stmt.name.pascalize»Record> select(ContentResolver resolver, String selection) {
				    return select(resolver, selection, null, null);
				}
				
				public static List<«stmt.name.pascalize»Record> select(ContentResolver resolver) {
				    return select(resolver, null, null, null);
				}
				
				public static «stmt.name.pascalize»Record selectFirst(ContentResolver resolver, SQuery query) {
					return selectFirst(resolver, query, null);
				}
				
				public static «stmt.name.pascalize»Record selectFirst(ContentResolver resolver, SQuery query, String sortOrder) {
				    Cursor c = null;
				    
				    try {
				        c = query.query(resolver, «stmt.name.pascalize».CONTENT_URI, PROJECTION, sortOrder);
				        
				        if(!c.moveToFirst()) {
				        	return null;
				        }
				        
				        return fromCursor(c);
				        
				    } finally {
				        Closeables.closeSilently(c);
				    }
				    
				    
				}
			}
		'''
		
		def generateProjectionArrayMembers(CreateTableStatement stmt) '''
			«FOR col : stmt.columnDefs SEPARATOR ','»
			«stmt.name.pascalize».«col.name.underscore.toUpperCase»
			«ENDFOR»
		'''

		def generateProjectionIndicesMembers(CreateTableStatement stmt) '''
			«var counter=-1»
			«FOR col : stmt.columnDefs»
				int «col.name.underscore.toUpperCase» = «counter = counter + 1»;
			«ENDFOR»
		'''
		
		def generateFields(CreateTableStatement stmt) '''
			«FOR col : stmt.columnDefs»
			private «col.type.toJavaTypeName» m«col.name.pascalize»;
			private boolean m«col.name.pascalize»Dirty;
			«ENDFOR»
		'''
		
		def generateAccessors(CreateTableStatement stmt) '''
			public void setId (long id) {
				m_id = id;
				m_idDirty = true;
			}
			
			public long getId() {
				return m_id;
			}
			
			«FOR col : stmt.columnDefs.filter([!it.name.equals("_id")])»
			public void set«col.name.pascalize»(«col.type.toJavaTypeName» «col.name.camelize») {
				m«col.name.pascalize» = «col.name.camelize»;
				m«col.name.pascalize»Dirty = true;
			}
			
			public «col.type.toJavaTypeName» get«col.name.pascalize»() {
				return m«col.name.pascalize»;
			}
			
			«ENDFOR»
		'''
}