package com.robotoworks.mechanoid.sqlite.generator

import com.robotoworks.mechanoid.sqlite.sqliteModel.CreateTableStatement
import com.robotoworks.mechanoid.sqlite.sqliteModel.Model

import static extension com.robotoworks.mechanoid.common.util.Strings.*
import static extension com.robotoworks.mechanoid.sqlite.generator.Extensions.*
import com.robotoworks.mechanoid.sqlite.sqliteModel.ColumnType

class ActiveRecordGenerator {
		def CharSequence generate(Model model, CreateTableStatement stmt) '''
			/*
			 * Generated by Robotoworks Mechanoid
			 */
			package «model.packageName»;

			import android.content.ContentResolver;
			import android.content.ContentUris;
			import android.database.Cursor;
			import android.net.Uri;
			import android.os.Parcel;
			import android.os.Parcelable;
			
			import «model.packageName».«model.database.name.pascalize»Contract.«stmt.name.pascalize»;
			import «model.packageName».«model.database.name.pascalize»Contract.«stmt.name.pascalize».Builder;
			import com.robotoworks.mechanoid.sqlite.SQuery;
			import com.robotoworks.mechanoid.util.Closeables;
			import com.robotoworks.mechanoid.sqlite.ActiveRecord;
			import com.robotoworks.mechanoid.Mechanoid;
			import com.robotoworks.mechanoid.content.MechanoidContentProvider;
			
			public class «stmt.name.pascalize»Record extends ActiveRecord implements Parcelable {
			    public static final Parcelable.Creator<«stmt.name.pascalize»Record> CREATOR 
			    	= new Parcelable.Creator<«stmt.name.pascalize»Record>() {
			        public «stmt.name.pascalize»Record createFromParcel(Parcel in) {
			            return new «stmt.name.pascalize»Record(in);
			        }
			
			        public «stmt.name.pascalize»Record[] newArray(int size) {
			            return new «stmt.name.pascalize»Record[size];
			        }
			    };
			    
			    public static String[] PROJECTION = {
			    	«generateProjectionArrayMembers(stmt)»
			    };
			    
			    public interface Indices {
			    	«generateProjectionIndicesMembers(stmt)»
			    }
			    
			    «generateFields(stmt)»
			    «generateAccessors(stmt)»
			    
			    public «stmt.name.pascalize»Record() {
				}
				
				private «stmt.name.pascalize»Record(Parcel in) {
					«var counter=-1»
					«FOR col : stmt.columnDefs»
					«IF col.type == ColumnType::BOOLEAN»
					m«col.name.pascalize» = (in.readInt() > 0);
					«ELSE»
					m«col.name.pascalize» = in.read«col.type.toJavaTypeName.pascalize»();
					«ENDIF»
					«ENDFOR»
					
					boolean[] dirtyFlags = new boolean[«stmt.columnDefs.size»];
					in.readBooleanArray(dirtyFlags);
					«FOR col : stmt.columnDefs»
					m«col.name.pascalize»Dirty = dirtyFlags[«counter = counter + 1»];
					«ENDFOR»
				}
				
				@Override
				public int describeContents() {
				    return 0;
				}
				
				@Override
				public void writeToParcel(Parcel dest, int flags) {
					«FOR col : stmt.columnDefs»
					«IF col.type == ColumnType::BOOLEAN»
					dest.writeInt(m«col.name.pascalize» ? 1 : 0);
					«ELSE»
					dest.write«col.type.toJavaTypeName.pascalize»(m«col.name.pascalize»);
					«ENDIF»
					«ENDFOR»
				    dest.writeBooleanArray(new boolean[] {
						«FOR col : stmt.columnDefs SEPARATOR ","»
						m«col.name.pascalize»Dirty
						«ENDFOR»
				    });
				}
				
				private Builder createBuilder() {
					Builder builder = «stmt.name.pascalize».newBuilder();

					«FOR col : stmt.columnDefs.filter([!it.name.equals("_id")])»
					if(m«col.name.pascalize»Dirty) {
						builder.set«col.name.pascalize»(m«col.name.pascalize»);
					}
					«ENDFOR»
					
					return builder;
				}
			    
			    @Override
				public long save(){
					Builder builder = createBuilder();
					
					if(m_id > 0) {
					    builder.update(m_id);
					} else {
					    Uri uri = builder.insert();
					    m_id = ContentUris.parseId(uri);
					}
					
					setDirty(false);
					
					return m_id;
				}
				
			    @Override
				public long save(boolean notifyChange){
					Builder builder = createBuilder();
					
					if(m_id > 0) {
					    builder.update(m_id, notifyChange);
					} else {
					    Uri uri = builder.insert(notifyChange);
					    m_id = ContentUris.parseId(uri);
					}
					
					setDirty(false);
					
					return m_id;
				}
				
			    @Override
				public int update(SQuery query){
					Builder builder = createBuilder();
					
					int affected = builder.update(query);
					
					setDirty(false);
					
					return affected;
				}
				
			    @Override
				public int update(SQuery query, boolean notifyChange){
					Builder builder = createBuilder();
					
					int affected = builder.update(query, notifyChange);
					
					setDirty(false);
					
					return affected;
				}
				
			    @Override
				public boolean delete(){
					ContentResolver resolver = Mechanoid.getContentResolver();
					
					boolean result = resolver.delete(
						«stmt.name.pascalize».CONTENT_URI.buildUpon()
						.appendPath(String.valueOf(m_id)).build(), null, null) > 0;
						
					setDirty(false);
					
					return result;
				}
				
			    @Override
				public boolean delete(boolean notifyChange){
					ContentResolver resolver = Mechanoid.getContentResolver();
					
					Uri uri = «stmt.name.pascalize».CONTENT_URI.buildUpon()
						.appendPath(String.valueOf(m_id))
						.appendQueryParameter(
							MechanoidContentProvider.PARAM_NOTIFY, 
							String.valueOf(notifyChange)).build();

					boolean result = resolver.delete(uri, null, null) > 0;
						
					setDirty(false);
					
					return result;
				}
				
			    @Override
				public void setDirty(boolean dirty){
					«FOR col : stmt.columnDefs»
					m«col.name.pascalize»Dirty = dirty;
					«ENDFOR»
				}
				
			    @Override
				public void reload(){
					if(m_id == 0) {
						return;
					}
				    
				    Cursor c = null;
				    
				    ContentResolver resolver = Mechanoid.getContentResolver();
				    
				    try {
				        c = resolver.query(«stmt.name.pascalize».CONTENT_URI.buildUpon()
						.appendPath(String.valueOf(m_id)).build(), PROJECTION, null, null, null);
				        
				        if(c.moveToFirst()) {
				        	setPropertiesFromCursor(c);
				        	setDirty(false);
				        }
				    } finally {
				        Closeables.closeSilently(c);
				    }
				}
				
				protected void setPropertiesFromCursor(Cursor c) {
					setId(c.getLong(Indices._ID));
					«FOR col : stmt.columnDefs.filter([!it.name.equals("_id")])»
					«IF col.type == ColumnType::BOOLEAN»
					set«col.name.pascalize»(c.getInt(Indices.«col.name.underscore.toUpperCase») > 0);
					«ELSE»
					set«col.name.pascalize»(c.get«col.type.toJavaTypeName.pascalize»(Indices.«col.name.underscore.toUpperCase»));
					«ENDIF»
					«ENDFOR»
				}
				
				public static «stmt.name.pascalize»Record fromCursor(Cursor c) {
				    «stmt.name.pascalize»Record item = new «stmt.name.pascalize»Record();
				    
					item.setPropertiesFromCursor(c);
					
					item.setDirty(false);
					
				    return item;
				}
				
				public static «stmt.name.pascalize»Record get(long id) {
				    Cursor c = null;
				    
				    ContentResolver resolver = Mechanoid.getContentResolver();
				    
				    try {
				        c = resolver.query(«stmt.name.pascalize».CONTENT_URI.buildUpon()
						.appendPath(String.valueOf(id)).build(), PROJECTION, null, null, null);
				        
				        if(!c.moveToFirst()) {
				            return null;
				        }
				        
				        return fromCursor(c);
				    } finally {
				        Closeables.closeSilently(c);
				    }
				}
			}
		'''
		
		def generateProjectionArrayMembers(CreateTableStatement stmt) '''
			«FOR col : stmt.columnDefs SEPARATOR ','»
			«stmt.name.pascalize».«col.name.underscore.toUpperCase»
			«ENDFOR»
		'''

		def generateProjectionIndicesMembers(CreateTableStatement stmt) '''
			«var counter=-1»
			«FOR col : stmt.columnDefs»
				int «col.name.underscore.toUpperCase» = «counter = counter + 1»;
			«ENDFOR»
		'''
		
		def generateFields(CreateTableStatement stmt) '''
			«FOR col : stmt.columnDefs»
			private «col.type.toJavaTypeName» m«col.name.pascalize»;
			private boolean m«col.name.pascalize»Dirty;
			«ENDFOR»
		'''
		
		def generateAccessors(CreateTableStatement stmt) '''
			public void setId (long id) {
				m_id = id;
				m_idDirty = true;
			}
			
			public long getId() {
				return m_id;
			}
			
			«FOR col : stmt.columnDefs.filter([!it.name.equals("_id")])»
			public void set«col.name.pascalize»(«col.type.toJavaTypeName» «col.name.camelize») {
				m«col.name.pascalize» = «col.name.camelize»;
				m«col.name.pascalize»Dirty = true;
			}
			
			public «col.type.toJavaTypeName» get«col.name.pascalize»() {
				return m«col.name.pascalize»;
			}
			
			«ENDFOR»
		'''
}