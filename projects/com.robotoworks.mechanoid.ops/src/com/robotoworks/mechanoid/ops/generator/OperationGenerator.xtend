package com.robotoworks.mechanoid.ops.generator


import static extension com.robotoworks.mechanoid.common.util.Strings.*
import static extension com.robotoworks.mechanoid.ops.generator.Extensions.*
import com.robotoworks.mechanoid.ops.opServiceModel.Model
import com.robotoworks.mechanoid.ops.opServiceModel.Operation

class OperationGenerator {
		def CharSequence generate(Model model, Operation op) '''
			/*
			 * Generated by Robotoworks Mechanoid
			 */
			package «model.packageName»;

			import com.robotoworks.mechanoid.ops.Operation;
			import android.content.Context;
			import android.content.Intent;
			import android.os.Bundle;
			
			public abstract class Abstract«op.name.pascalize»Operation extends Operation {
				public static final String ACTION_«op.name.underscore.toUpperCase» = "«model.packageName».actions.«op.name.underscore.toUpperCase»";
			
				«FOR arg : op.args»
				public static final String EXTRA_«arg.name.underscore.toUpperCase» = "«model.packageName».extras.«arg.name.underscore.toUpperCase»";
				«ENDFOR»
			
				«FOR arg : op.args»
				private «arg.type.toTypeLiteral» m«arg.name.pascalize»;
				«ENDFOR»
			
				«FOR arg : op.args»
				public «arg.type.toTypeLiteral» get«arg.name.pascalize»() {
					return m«arg.name.pascalize»;
				}
				«ENDFOR»
			
				public static final Intent create«op.name.pascalize»Intent(Context context«FOR arg: op.args», «arg.type.toTypeLiteral» «arg.name.camelize»«ENDFOR») {
					Intent intent = new Intent(ACTION_«op.name.underscore.toUpperCase»);
					
					Bundle extras = new Bundle();
					«FOR arg : op.args»
					extras.«arg.type.toBundlePutMethodName»(EXTRA_«arg.name.underscore.toUpperCase», «arg.name.camelize»);
					«ENDFOR»
					
					intent.putExtras(extras);
					
					return intent;
					
				}
			
				@Override
				public Bundle execute() {
					Bundle extras = getIntent().getExtras();
					«FOR arg : op.args»
					m«arg.name.pascalize» = extras.«arg.type.toBundleGetMethodName»(EXTRA_«arg.name.underscore.toUpperCase»);
					«ENDFOR»
					
					return onExecute();
				}
						
				protected abstract Bundle onExecute();
			}
			'''
			
		def CharSequence generateStub(Model model, Operation op) '''
			/*
			 * Generated by Robotoworks Mechanoid
			 */
			package «model.packageName»;
			
			import «model.packageName».Abstract«op.name.pascalize»Operation;
			import android.content.Context;
			import android.content.Intent;
			import android.os.Bundle;
			
			public class «op.name.pascalize»Operation extends Abstract«op.name.pascalize»Operation {
				@Override
				protected Bundle onExecute() {
					// TODO Auto-generated method stub
					return null;
				}
			}
		'''
}