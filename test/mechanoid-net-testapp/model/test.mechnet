package com.mechanoid.net

enum generate Day {
	monday,
	tuesday,
	wednesday,
	thursday,
	friday
}

entity generate Item {
	a:String,
	b:int,
	c:long,
	d:double,
	e:boolean	
}

entity generate Node {
	name:String,
	children:Node[]
}

entity HeaderItem {
	key:String,
	value:String
}

entity generate A {
	name:String,
	b:B
}

entity generate B {
	name:String,
	a:A
}

client TestService "http://10.0.2.2:8080/mechanoid-net-service/test" {
	headers
		"X-A":"A",
		"X-B":"B"
		
	get getStringArray "/stringArray" {
		response String[]
	}
	
	get getIntArray "/intArray" {
		response int[]
	}
	
	get getLongArray "/longArray" {
		response long[]
	}
	
	get getDoubleArray "/doubleArray" {
		response double[]
	}
	
	get getBooleanArray "/booleanArray" {
		response boolean[]
	}
	
	get getEnumArray "/enumArray" {
		response Day[]
	}
	
	get getItem "/item" {
		response Item
	}
	
	get getNodes "/nodes" {
		response Node[]
	}
	
	get echoParams "/echoParams" {
		params a:String, b:boolean, c:double, d:int, e:long
		response String[]
	}
	
	get skipping "/skipping" {
		response {
			"outer" {
				"inner" {
					a:String,
					b:int,
					c:long
				}	
			}
		}
	}
	
	get skippingWithItem "/skipping" {
		response {
			"outer" {
				"inner":Item	
			}
		}
	}
	
	get getAB "/ab" {
		response A
	}
	
	post skippingPost "/echoJson" {
		body {
			"outer" {
				"inner" {
					a:String,
					b:int,
					c:long
				}	
			}
		}
		response {
			"outer" {
				"inner" {
					a:String,
					b:int,
					c:long
				}	
			}
		}
	}
	
	post postInt "/echo" {
		body int
		response int
	}
	
	post postString "/echo" {
		body String
		response String
	}
	
	post postStringArray "/echo" {
		body String[]
		response String[]
	}
	
	get echoSegments "/echoUrl/:a/:b/:c" {
		response String
	}
	
	get echoHeaders '/echoHeaders' {
		headers
			"X-C":"C"
		response HeaderItem[]
	}
	
	
}