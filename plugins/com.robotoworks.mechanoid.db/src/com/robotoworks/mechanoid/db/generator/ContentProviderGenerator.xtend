package com.robotoworks.mechanoid.db.generator

import com.robotoworks.mechanoid.db.sqliteModel.ActionStatement
import com.robotoworks.mechanoid.db.sqliteModel.Model

import static extension com.robotoworks.mechanoid.text.Strings.*
import static extension com.robotoworks.mechanoid.db.util.ModelUtil.*
import com.robotoworks.mechanoid.db.sqliteModel.ContentUri
import com.robotoworks.mechanoid.db.sqliteModel.ContentUriParamSegment

class ContentProviderGenerator {
		def CharSequence generate(Model model, SqliteDatabaseSnapshot snapshot) '''
			/*
			 * Generated by Robotoworks Mechanoid
			 */
			package «model.packageName»;
			
			import android.content.Context;
			import android.content.UriMatcher;
			import android.net.Uri;
			import java.util.Set;
			import com.robotoworks.mechanoid.db.MechanoidContentProvider;
			import com.robotoworks.mechanoid.db.MechanoidSQLiteOpenHelper;
			import com.robotoworks.mechanoid.db.DefaultContentProviderActions;
			import com.robotoworks.mechanoid.db.ContentProviderActions;
			import «model.packageName».Abstract«model.database.name.pascalize»OpenHelper.Sources;
			«FOR tbl : snapshot.tables.filter([it.hasAndroidPrimaryKey])»
			import «model.packageName».«tbl.name.pascalize»Record;
			«ENDFOR»			
			
			public abstract class Abstract«model.database.name.pascalize»ContentProvider extends MechanoidContentProvider {
			
				«var counter=-1»
				«FOR tbl : snapshot.tables»
				public static final int «tbl.name.underscore.toUpperCase» = «counter=counter+1»;
				«IF tbl.hasAndroidPrimaryKey»
				public static final int «tbl.name.underscore.toUpperCase»_ID = «counter=counter+1»;
				«ENDIF»
				«ENDFOR»

				«FOR vw : snapshot.views»
				public static final int «vw.name.underscore.toUpperCase» = «counter=counter+1»;
				«IF vw.hasAndroidPrimaryKey»
				public static final int «vw.name.underscore.toUpperCase»_ID = «counter=counter+1»;
				«ENDIF»				
				«ENDFOR»
				
				«FOR tbl : model.configInitTables»
				public static final int «tbl.name.underscore.toUpperCase» = «counter=counter+1»;
				«IF tbl.hasAndroidPrimaryKey»
				public static final int «tbl.name.underscore.toUpperCase»_ID = «counter=counter+1»;
				«ENDIF»
				«ENDFOR»

				«FOR vw : model.configInitViews»
				public static final int «vw.name.underscore.toUpperCase» = «counter=counter+1»;
				«IF vw.hasAndroidPrimaryKey»
				public static final int «vw.name.underscore.toUpperCase»_ID = «counter=counter+1»;
				«ENDIF»				
				«ENDFOR»
				
				«IF model.database.config !=null»
				«FOR a : model.database.config.statements.filter([it instanceof ActionStatement])»
				public static final int «(a as ActionStatement).uri.type.underscore.toUpperCase»_«(a as ActionStatement).name.underscore.toUpperCase» = «counter=counter+1»;
				«ENDFOR»
				«ENDIF»			
				public static final int NUM_URI_MATCHERS = «counter + 1»;
			
				@Override
			    protected UriMatcher createUriMatcher() {
			        final UriMatcher matcher = new UriMatcher(UriMatcher.NO_MATCH);
			        final String authority = «model.database.name.pascalize»Contract.CONTENT_AUTHORITY;
			
					«FOR tbl : snapshot.tables»
					matcher.addURI(authority, "«tbl.name»", «tbl.name.underscore.toUpperCase»);
					«IF tbl.hasAndroidPrimaryKey»
					matcher.addURI(authority, "«tbl.name»/#", «tbl.name.underscore.toUpperCase»_ID);
					«ENDIF»
					«ENDFOR»
					«FOR vw : snapshot.views»
					matcher.addURI(authority, "«vw.name»", «vw.name.underscore.toUpperCase»);
					«IF vw.hasAndroidPrimaryKey»
					matcher.addURI(authority, "«vw.name»/#", «vw.name.underscore.toUpperCase»_ID);
					«ENDIF»
					«ENDFOR»
					«FOR tbl : model.configInitTables»
					matcher.addURI(authority, "«tbl.name»", «tbl.name.underscore.toUpperCase»);
					«IF tbl.hasAndroidPrimaryKey»
					matcher.addURI(authority, "«tbl.name»/#", «tbl.name.underscore.toUpperCase»_ID);
					«ENDIF»
					«ENDFOR»
					«FOR vw : model.configInitViews»
					matcher.addURI(authority, "«vw.name»", «vw.name.underscore.toUpperCase»);
					«IF vw.hasAndroidPrimaryKey»
					matcher.addURI(authority, "«vw.name»/#", «vw.name.underscore.toUpperCase»_ID);
					«ENDIF»
					«ENDFOR»

					// User Actions
					«IF model.database.config !=null»
					«FOR a : model.database.config.statements.filter([it instanceof ActionStatement])»
					«var stmt = a as ActionStatement»
					matcher.addURI(authority, "«stmt.uri.asString»", «(a as ActionStatement).uri.type.underscore.toUpperCase»_«(a as ActionStatement).name.underscore.toUpperCase»); 
					«ENDFOR»
					«ENDIF»
			        return matcher;
			    }
			    
			    @Override
			    protected String[] createContentTypes() {
					String[] contentTypes = new String[NUM_URI_MATCHERS];

					«FOR tbl : snapshot.tables»
					contentTypes[«tbl.name.underscore.toUpperCase»] = «model.database.name.pascalize»Contract.«tbl.name.pascalize».CONTENT_TYPE;
					«IF tbl.hasAndroidPrimaryKey»
					contentTypes[«tbl.name.underscore.toUpperCase»_ID] = «model.database.name.pascalize»Contract.«tbl.name.pascalize».ITEM_CONTENT_TYPE;
					«ENDIF»
					«ENDFOR»
					«FOR vw : snapshot.views»
					contentTypes[«vw.name.underscore.toUpperCase»] = «model.database.name.pascalize»Contract.«vw.name.pascalize».CONTENT_TYPE;
					«IF vw.hasAndroidPrimaryKey»
					contentTypes[«vw.name.underscore.toUpperCase»_ID] = «model.database.name.pascalize»Contract.«vw.name.pascalize».ITEM_CONTENT_TYPE;
					«ENDIF»
					«ENDFOR»	
					«FOR tbl : model.configInitTables»
					contentTypes[«tbl.name.underscore.toUpperCase»] = «model.database.name.pascalize»Contract.«tbl.name.pascalize».CONTENT_TYPE;
					«IF tbl.hasAndroidPrimaryKey»
					contentTypes[«tbl.name.underscore.toUpperCase»_ID] = «model.database.name.pascalize»Contract.«tbl.name.pascalize».ITEM_CONTENT_TYPE;
					«ENDIF»
					«ENDFOR»
					«FOR vw : model.configInitViews»
					contentTypes[«vw.name.underscore.toUpperCase»] = «model.database.name.pascalize»Contract.«vw.name.pascalize».CONTENT_TYPE;
					«IF vw.hasAndroidPrimaryKey»
					contentTypes[«vw.name.underscore.toUpperCase»_ID] = «model.database.name.pascalize»Contract.«vw.name.pascalize».ITEM_CONTENT_TYPE;
					«ENDIF»
					«ENDFOR»	
					«IF model.database.config !=null»
					«FOR a : model.database.config.statements.filter([it instanceof ActionStatement])»
					contentTypes[«(a as ActionStatement).uri.type.underscore.toUpperCase»_«(a as ActionStatement).name.underscore.toUpperCase»] = «(a as ActionStatement).generateContentTypeConstantReference(model)»;
					«ENDFOR»
					«ENDIF»	
					
					return contentTypes;
			    }
			
				@Override
				protected MechanoidSQLiteOpenHelper createOpenHelper(Context context) {
			        return new «model.database.name.pascalize»OpenHelper(context);
				}
				
				@Override
				protected Set<Uri> getRelatedUris(Uri uri) {
					return «model.database.name.pascalize»Contract.REFERENCING_VIEWS.get(uri);
				}
			    
			    @Override
			    protected ContentProviderActions createActions(int id) {
			    	switch(id) {
						«FOR tbl : snapshot.tables»
						case «tbl.name.underscore.toUpperCase»: 
							return create«tbl.name.pascalize»Actions();
						«IF tbl.hasAndroidPrimaryKey»
						case «tbl.name.underscore.toUpperCase»_ID:
							return create«tbl.name.pascalize»ByIdActions();
						«ENDIF»
						«ENDFOR»
						«FOR vw : snapshot.views»
						case «vw.name.underscore.toUpperCase»:
							return create«vw.name.pascalize»Actions();
						«IF vw.hasAndroidPrimaryKey»
						case «vw.name.underscore.toUpperCase»_ID: 
							return create«vw.name.pascalize»ByIdActions();
						«ENDIF»
						«ENDFOR»
						«FOR tbl : model.configInitTables»
						case «tbl.name.underscore.toUpperCase»: 
							return create«tbl.name.pascalize»Actions();
						«IF tbl.hasAndroidPrimaryKey»
						case «tbl.name.underscore.toUpperCase»_ID:
							return create«tbl.name.pascalize»ByIdActions();
						«ENDIF»
						«ENDFOR»
						«FOR vw : model.configInitViews»
						case «vw.name.underscore.toUpperCase»:
							return create«vw.name.pascalize»Actions();
						«IF vw.hasAndroidPrimaryKey»
						case «vw.name.underscore.toUpperCase»_ID: 
							return create«vw.name.pascalize»ByIdActions();
						«ENDIF»
						«ENDFOR»
						«IF model.database.config !=null»
						«FOR a : model.database.config.statements.filter([it instanceof ActionStatement])»
						case «(a as ActionStatement).uri.type.underscore.toUpperCase»_«(a as ActionStatement).name.underscore.toUpperCase»:
							return create«(a as ActionStatement).name.pascalize»Actions();
						«ENDFOR»
						«ENDIF»
						default:
							throw new UnsupportedOperationException("Unknown id: " + id);
			    	}
			    }
			    
			    «FOR tbl:snapshot.tables»
			    «IF tbl.hasAndroidPrimaryKey»
			    protected ContentProviderActions create«tbl.name.pascalize»ByIdActions() {
			    	return new DefaultContentProviderActions(Sources.«tbl.name.underscore.toUpperCase», true, «IF tbl.hasAndroidPrimaryKey»«tbl.name.pascalize»Record.getFactory()«ELSE»null«ENDIF»);
			    }
			    
			    «ENDIF»
			    protected ContentProviderActions create«tbl.name.pascalize»Actions() {
			    	return new DefaultContentProviderActions(Sources.«tbl.name.underscore.toUpperCase», false, «IF tbl.hasAndroidPrimaryKey»«tbl.name.pascalize»Record.getFactory()«ELSE»null«ENDIF»);
			    }
			    
			    «ENDFOR»
			    «FOR view:snapshot.views»
			    «IF view.hasAndroidPrimaryKey»
			    protected ContentProviderActions create«view.name.pascalize»ByIdActions() {
			    	return new DefaultContentProviderActions(Sources.«view.name.underscore.toUpperCase», true, «IF view.hasAndroidPrimaryKey»«view.name.pascalize»Record.getFactory()«ELSE»null«ENDIF»);
			    }
			    
			    «ENDIF»
			    protected ContentProviderActions create«view.name.pascalize»Actions() {
			    	return new DefaultContentProviderActions(Sources.«view.name.underscore.toUpperCase», false, «IF view.hasAndroidPrimaryKey»«view.name.pascalize»Record.getFactory()«ELSE»null«ENDIF»);
			    }
			    
			    «ENDFOR»
			    «FOR tbl:model.configInitTables»
			    «IF tbl.hasAndroidPrimaryKey»
			    protected ContentProviderActions create«tbl.name.pascalize»ByIdActions() {
			    	return new DefaultContentProviderActions(Sources.«tbl.name.underscore.toUpperCase», true, «IF tbl.hasAndroidPrimaryKey»«tbl.name.pascalize»Record.getFactory()«ELSE»null«ENDIF»);
			    }
			    
			    «ENDIF»
			    protected ContentProviderActions create«tbl.name.pascalize»Actions() {
			    	return new DefaultContentProviderActions(Sources.«tbl.name.underscore.toUpperCase», false, «IF tbl.hasAndroidPrimaryKey»«tbl.name.pascalize»Record.getFactory()«ELSE»null«ENDIF»);
			    }
			    
			    «ENDFOR»
			    «FOR view:model.configInitViews»
			    «IF view.hasAndroidPrimaryKey»
			    protected ContentProviderActions create«view.name.pascalize»ByIdActions() {
			    	return new DefaultContentProviderActions(Sources.«view.name.underscore.toUpperCase», true, «IF view.hasAndroidPrimaryKey»«view.name.pascalize»Record.getFactory()«ELSE»null«ENDIF»);
			    }
			    
			    «ENDIF»
			    protected ContentProviderActions create«view.name.pascalize»Actions() {
			    	return new DefaultContentProviderActions(Sources.«view.name.underscore.toUpperCase», false, «IF view.hasAndroidPrimaryKey»«view.name.pascalize»Record.getFactory()«ELSE»null«ENDIF»);
			    }
			    
			    «ENDFOR»
				«IF model.database.config !=null»
				«FOR a : model.database.config.statements.filter(typeof(ActionStatement))»
				protected ContentProviderActions create«a.name.pascalize»Actions() {
					return new ContentProviderActions();
				}
				
				«ENDFOR»
				«ENDIF»
			}
		'''
		
		def asString(ContentUri uri) {
			var builder = new StringBuilder()
			
			builder.append(uri.type)
			
			for(seg : uri.segments) {
				builder.append("/")
				if(seg instanceof ContentUriParamSegment) {
					var paramSeg = seg as ContentUriParamSegment
					
					if(paramSeg.num) {
						builder.append("#")
					} else {
						builder.append("*")
					}
					
				} else {
					builder.append(seg.name)
				}
			}
			
			return builder.toString
		}

	
		def generateContentTypeConstantReference(ActionStatement action, Model model) {
			return model.database.name.pascalize + "Contract." + action.uri.type.pascalize + ".CONTENT_TYPE";
		}

			
		def CharSequence generateStub(Model model, SqliteDatabaseSnapshot snapshot) '''
			/*******************************************************************************
			 * Copyright (c) 2012, Robotoworks Limited
			 * All rights reserved. This program and the accompanying materials
			 * are made available under the terms of the Eclipse Public License v1.0
			 * which accompanies this distribution, and is available at
			 * http://www.eclipse.org/legal/epl-v10.html
			 * 
			 *******************************************************************************/
			package «model.packageName»;
			
			import «model.packageName».Abstract«model.database.name.pascalize»ContentProvider;
			
			public class «model.database.name.pascalize»ContentProvider extends Abstract«model.database.name.pascalize»ContentProvider {}
		'''
}