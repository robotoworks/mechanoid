grammar com.robotoworks.mechanoid.sharedprefs.SharedPreferencesModel with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate sharedPreferencesModel "http://www.robotoworks.com/mechanoid/sharedprefs/SharedPreferencesModel"

Model:
	'package' packageName=FQN
	preferencesBlock=PreferencesBlock;

PreferencesBlock:
	'preferences' name=ID '{' (prefs+=Preference)* '}';

Preference:
	name=ID ':' type=PreferenceType ("=" defaultValue=Literal)?
;

Literal:
	{BooleanLiteral} literal=BooleanValue |
	{StringLiteral} literal=STRING |
	{NumericLiteral} literal=SignedNumber
;

enum PreferenceType:
	String='String' | 
	Integer='int' | 
	Boolean='boolean' | 
	Float='float' |
	Long='long'
	;

enum BooleanValue:
	true|false
;

SignedNumber returns ecore::EBigDecimal:
'-'?NUMBER
;

terminal NUMBER returns ecore::EBigDecimal:
	('0'..'9')* ('.' ('0'..'9')+)?;

terminal INT returns ecore::EInt: 
	"$$$don't use this anymore$$$";
	
FQN:
	ID (=>'.' ID)*;